plugins {
    id 'java'
    id 'eclipse'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
	apt
}

dependencies {
    implementation 'org.openapitools:openapi-generator-core:5.3.0'
    implementation 'org.openapitools:openapi-generator:5.3.0'
	implementation 'io.swagger.parser.v3:swagger-parser:2.0.26'

	apt        'org.immutables:value:2.8.2'
	compileOnly         'org.immutables:value:2.8.2:annotations'
	annotationProcessor 'org.immutables:value:2.8.2'

    implementation 'org.slf4j:slf4j-jdk14:2.0.0-alpha5'

	testImplementation(platform('org.junit:junit-bom:5.8.2'))
	testImplementation('org.junit.jupiter:junit-jupiter')
   	testImplementation('org.junit.jupiter:junit-jupiter-params')
	testImplementation('org.junit.jupiter:junit-jupiter-engine')
	testImplementation('org.junit.jupiter:junit-jupiter-api')
	testImplementation('org.junit.platform:junit-platform-runner')
	testImplementation("org.assertj:assertj-core:3.21.0")
}

group 'dk.mada'
version '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

test {
	useJUnitPlatform()
	
	systemProperty("run_all_tests", "true")
}

tasks.eclipse.doLast {
	String valueJarPath = configurations.apt.getSingleFile().getAbsolutePath()
	
	File factoryPath = file(".factorypath")
	factoryPath.delete()
	factoryPath << """<factorypath>
    <factorypathentry kind="EXTJAR" id="${valueJarPath}" enabled="true" runInBatchMode="false"/>
</factorypath>
"""
}
